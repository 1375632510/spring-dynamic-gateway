plugins {
    id "com.livk.dependency"
    id "org.springframework.boot" apply(false)
}

ext {
    bom = project(":livk-dependencies")
    gradleModuleProjects = subprojects.findAll {
        it.buildFile.exists()
    } - bom
    commonModuleProjects = gradleModuleProjects.findAll {
        it.name.startsWith("livk-common-") || it.name.endsWith("-api")
    }
    springModuleProjects = gradleModuleProjects - commonModuleProjects
}

configure(commonModuleProjects) {
    apply plugin: "com.livk.maven.deployed"
    apply plugin: "com.livk.resources"
}

configure(springModuleProjects) {
    apply plugin: "com.livk.build.boot"

    dependencies {
        implementation "org.springframework.cloud:spring-cloud-starter-consul-discovery"
    }
}

configure(allprojects) {
    apply plugin: "com.livk.clean.expand"
    apply plugin: "com.livk.all.configuration"

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/release" }
    }
}

configure(gradleModuleProjects) {
    apply plugin: "com.livk.compile-args"
    apply plugin: "com.livk.dependency"

    dependencies {
        dependencyBom platform(project(":livk-dependencies"))

        implementation "com.google.code.findbugs:annotations"

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
}
